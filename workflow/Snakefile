import pandas as pd
import math
import sys
import os
from snakemake.utils import min_version

min_version("7.32.3")

# Force users to use the same underlying OS via singularity.
# container: "docker://condaforge/mambaforge:23.3.1-1"

ref = config["ref"]
ref_name = config["ref_name"]
n_chunks = config["n_chunks"]
chunks = [f"{i+1}-of-{n_chunks}" for i in range(n_chunks)]

dhs = config.get("dhs", workflow.source_path("annotations/GM12878_DHS.bed.gz"))
excludes = config.get("excludes", [])
if ref_name == "hg38" or ref_name == "GRCh38":
    workflow / annotations / GM12878_DHS.bed.gz
    files = [
        "annotations/hg38.blacklist.ENCFF356LFX.bed.gz",
        "annotations/hg38.gap.bed.gz",
        "annotations/SDs.merged.hg38.bed.gz",
    ]
    excludes += [workflow.source_path(file) for file in files]

max_t = config.get("max_t", 4)
max_bins = config.get("max_bins", 75)
keep_chrs = config.get("keep_chromosomes", ".*")
force_coverage = config.get("force_coverage", None)

# coverage requirements
min_coverage = config.get("min_coverage", 4)
coverage_within_n_sd = config.get("coverage_within_n_sd", 5)

# sample, haplotype, and chromosome wildcard building
fai = pd.read_csv(f"{ref}.fai", sep="\t", names=["chr", "length", "x", "y", "z"])
conda = config.get("env", "../envs/env.yaml")
bins = list(range(1, max_bins + 1))
data = pd.read_csv(config["manifest"], sep="\s+", comment="#").set_index("sample")
haps = ["all", "hap1", "hap2", "unk"]
not_all = ["hap1", "hap2", "unk"]
not_unk = ["all", "hap1", "hap2"]
min_fire_fdr = config.get("min_fire_fdr", 0.10)
max_peak_fdr = config.get("max_peak_fdr", 0.05)
print(data.index)

types = ["fdr", "acc", "link", "nuc"]
types_to_col = {"fdr": 4, "acc": 5, "link": 6, "nuc": 7}

bw_types = ["score", "FDR", "log_FDR"]
bw_types = bw_types + [f"{t}_H1" for t in bw_types] + [f"{t}_H2" for t in bw_types]
el_types = ["fire", "linker", "nucleosome"]


include: "rules/common.smk"
include: "rules/coverages.smk"
include: "rules/train-model.smk"
include: "rules/apply-model.smk"
include: "rules/FDR-peaks.smk"
include: "rules/peak-stats.smk"
include: "rules/decorated-reads.smk"
include: "rules/track-hub.smk"


chroms = get_chroms()


wildcard_constraints:
    chrom="|".join(chroms),
    call="|".join(["msp", "m6a"]),
    chunk="|".join(chunks),
    sm="|".join(data.index),
    types="|".join(types),
    fdr="\d+",
    hp="|".join(haps),
    col="|".join(bw_types),
    el_type="|".join(el_types),


localrules:
    bed_chunks,
    dhs_null,
    model_input,
    make_model,
    merge_model_results,
    model_bam,
    trackhub,
    coverage,
    clustering_vs_null,
    percent_in_clusters,
    hap_differences,
    peaks_vs_percent,


rule all:
    input:
        # coverage information
        expand(rules.genome_bedgraph.output, sm=data.index),
        expand(rules.coverage.output, sm=data.index),
        expand(rules.exclude_from_shuffle.output, sm=data.index),
        # model results
        expand(rules.merge_model_results.output, sm=data.index),
        expand(rules.index_model_results.output, sm=data.index),
        expand(rules.fire_sites.output, sm=data.index),
        # fiber locations
        expand(rules.fiber_locations.output, sm=data.index),
        expand(rules.filtered_and_shuffled_fiber_locations.output, sm=data.index),
        # coverage of elements
        expand(
            rules.element_coverages.output,
            sm=data.index,
            hp=not_unk,
            el_type=el_types,
        ),
        # FDR results
        expand(rules.fdr_track.output, sm=data.index),
        expand(rules.fdr_peaks_by_fire_elements.output, sm=data.index),
        expand(rules.wide_fdr_peaks.output, sm=data.index),
        expand(rules.peaks_vs_percent.output, sm=data.index),
        expand(rules.one_percent_fdr_peaks.output, sm=data.index),
        # trackhub
        expand(rules.fdr_track_to_bw.output.bw, sm=data.index, col=bw_types),
        expand(rules.fdr_peaks_by_fire_elements_to_bb.output.bb, sm=data.index),
        expand(rules.percent_accessible.output.bw, hp=not_unk, sm=data.index),
        expand(
            rules.element_coverages_bw.output.bw,
            sm=data.index,
            hp=not_unk,
            el_type=el_types,
        ),
        expand(rules.trackhub.output, sm=data.index),
        expand(rules.decorate_fibers.output, sm=data.index),
